buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.0.3.RELEASE'
    }
}
plugins {
    id 'com.moowork.node' version '1.2.0'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'
apply plugin: 'findbugs'
apply plugin: 'checkstyle'

group = 'org.sgc'
version = '0.8.0-SNAPSHOT'

repositories {
    mavenCentral()
}

node {
    version = '8.9.4'
    npmVersion = '5.6.0'
    download = true

    // Node and npm should work in our 'frontend' folder
    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")
    yarnWorkDir = file("${project.buildDir}/yarn")

    // Where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/src/main/frontend")
}

dependencies {

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-security'

    compile 'org.slf4j:slf4j-api'
    compile 'ch.qos.logback:logback-classic'
    compile 'ch.qos.logback:logback-core'

    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'org.postgresql:postgresql'
    compile 'org.apache.xmlgraphics:batik-transcoder:1.10'
    compile 'org.apache.xmlgraphics:batik-codec:1.10'
    compile 'com.h2database:h2'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
}

wrapper {
    gradleVersion = '4.8'
}

checkstyle {
    configFile file('config/checkstyle/checkstyle.xml');
    toolVersion '8.10';
}

idea {
    module {
        downloadSources = true
    }
}

sourceSets {
    integrationTest {
        java.srcDirs = [ 'src/integration-test/java' ]
        resources.srcDirs = [ 'src/integration-test/resources' ]
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

clean.doLast {
    delete "${rootDir}/aws-archive.zip"
}

task webpack(type: NpmTask, dependsOn: 'npmInstall') {
    execOverrides {
        it.workingDir = "${project.projectDir}/src/main/frontend/"
    }
    args = [ 'run', File.separator == '\\' ? 'build-windows' : 'build' ]
}

processResources.dependsOn 'webpack'

task webpackWatch(type: NpmTask) {
    args = [ 'run', 'watch' ]
}

task copyStaticResourcesToBuildWatch(type: NpmTask) {
    args = [ 'run', 'watch-static-into-build' ]
}

task makeAwsArchive(type: Zip) {
    from "${rootDir}/build/libs/"
    include 'random-acts-of-kinase*.jar'
    from '.'
    include 'Procfile'
    include '.ebextensions/**'
    archiveName 'aws-archive.zip'
    destinationDir(file("${rootDir}"))
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

bootRun {
    if (System.getProperty('spring.profiles.active') == null) {
        println 'Defaulting to dev Spring profile'
        System.setProperty('spring.profiles.active', 'dev')
    }
    // Forward -D properties to the Spring Boot app
    systemProperties = System.properties
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}
