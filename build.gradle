buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:2.1.9.RELEASE'
    }
}
plugins {
    id 'com.moowork.node' version '1.3.1'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.4'
    id 'com.github.spotbugs' version '2.0.0'
}
apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.moowork.node'
apply plugin: 'com.github.spotbugs'
//apply plugin: 'checkstyle'

group = 'org.sgc'
version = '1.2.0-SNAPSHOT'

repositories {
    mavenCentral()
}

node {
    version = '10.14.1'
    npmVersion = '6.4.1'
    download = true

    // Node and npm should work in our 'frontend' folder
    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")
    yarnWorkDir = file("${project.buildDir}/yarn")

    // Where node_modules should be located
    nodeModulesDir = file("${project.projectDir}/src/main/frontend")
}

dependencies {

    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-devtools'
    compile 'org.springframework.boot:spring-boot-starter-actuator'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa'
    compile 'org.springframework.boot:spring-boot-starter-security'

    compile 'org.slf4j:slf4j-api'
    compile 'ch.qos.logback:logback-classic'
    compile 'ch.qos.logback:logback-core'

    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'
    compile 'org.apache.commons:commons-lang3:3.7'
    compile 'org.postgresql:postgresql'
    compile 'org.apache.xmlgraphics:batik-transcoder:1.11'
    compile 'org.apache.xmlgraphics:batik-codec:1.11'
    compile 'com.h2database:h2'

    testCompile 'org.springframework.boot:spring-boot-starter-test'
    testCompile 'org.springframework.security:spring-security-test'
}

wrapper {
    gradleVersion = '5.6.2'
}

//checkstyle {
//    toolVersion '8.15'
//}

idea {
    module {
        downloadSources = true
    }
}

compileJava {
//    sourceCompatibility javaVersion
//    targetCompatibility javaVersion
    options.debug = true
    options.debugOptions.debugLevel = 'source,vars,lines'
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

compileTestJava {
//    sourceCompatibility javaVersion
//    targetCompatibility javaVersion
    options.debug = true
    options.debugOptions.debugLevel = 'source,vars,lines'
    options.compilerArgs << '-Xlint:deprecation' << '-Xlint:unchecked'
}

sourceSets {
    integrationTest {
        java.srcDirs = [ 'src/integration-test/java' ]
        resources.srcDirs = [ 'src/integration-test/resources' ]
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

clean.doLast {
    // Must disambiguate with project.delete since we're doing this in clean's doLast
    project.delete "${rootDir}/aws-archive.zip"
}

task webpack(type: NpmTask, dependsOn: 'npmInstall') {
    execOverrides {
        it.workingDir = "${project.projectDir}/src/main/frontend/"
    }
    args = [ 'run', 'build' ]
}

processResources.dependsOn 'webpack'

// Generate SVG files for each compound in the local database.
// If you don't have openbabel set up, you can comment out this section.
// In that case the app will be built but without images for compounds.
task createSmileSvgs(type: Exec) {
    workingDir "${rootDir}"
    commandLine '/bin/sh', '-c', './util/create-smiles-svgs.sh'
}
processResources.dependsOn 'createSmileSvgs'
createSmileSvgs.dependsOn 'webpack' // Ensure smiles generation occurs after webpack build

task webpackWatch(type: NpmTask) {
    args = [ 'run', 'watch' ]
}

task copyStaticResourcesToBuildWatch(type: NpmTask) {
    args = [ 'run', 'watch-static-into-build' ]
}

task makeAwsArchive(type: Zip) {
    from "${rootDir}/build/libs/"
    include 'random-acts-of-kinase*.jar'
    from '.'
    include 'Procfile'
    include '.ebextensions/**'
    archiveName 'aws-archive.zip'
    destinationDir(file("${rootDir}"))
}

spotbugs {
    includeFilter = file('spotbugs-exclude.xml')
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

bootRun {

    jvmArgs = [ '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=10356' ]

    if (System.getProperty('spring.profiles.active') == null) {
        println 'Defaulting to dev Spring profile'
        System.setProperty('spring.profiles.active', 'dev')
    }
    // Forward -D properties to the Spring Boot app
    systemProperties = System.properties
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}
